// Logic Name
coco DReddit

////////////////////////
////// Logic State 
//////////////////////
state persistent:
    posts []Post
    userVotes Map[Address]Map[U64]U64 // 0 for not voted, 1 for upvoted, 2 for downvoted

////////////////////////
////// Classes
//////////////////////

// The class Todo is a structure for defining a Todo,
// where it can possess multiple fields and methods
class Post:
    field id U64
    field creator String
    field imageUri String
    field content String
    field upvotes U64
    field downvotes U64

////////////////////////
////// Deployer Endpoints
//////////////////////

// Init() deployer is a constructor that leaves
// the persistent state untouched at the deploy time.
endpoint deployer Init():
    pass

// pass means do nothing

////////////////////////
////// Write Endpoints
//////////////////////

// "" after endpoint name means endpoint mutates
// the logic state 

// CreatePost() is an invokable that creates & adds a new post
// and also returns the new post
endpoint invokable persistent CreatePost(creator, imageUri, content String) -> (post Post):
    if(creator == "" || content == ""):
        throw "Invalid Input"

    memory newPost = Post{
        creator: creator,
        imageUri: imageUri,
        content: content,
        upvotes: 0,
        downvotes: 0,
    }

    // "<-" is used to read and then mutate the state
    // (Here the final value of posts in the context 
    // block will be assigned to DReddit.State.posts)
    mutate posts <- DReddit.State.posts:
        newPost.id = len(posts)
        disperse append(posts, newPost)
        yield post newPost // To return new Post

// Upvote() is an invokable persistent to upvote
// the post with given id
endpoint invokable persistent Upvote(postId U64):
    memory user Address = Address(Sender)
    
    mutate posts, userVotes <- DReddit.State.posts, DReddit.State.userVotes:
        memory userVote = userVotes[user][postId]

        if(userVote == 1):
                throw "Already Upvoted"

        else if(userVote == 2):
                posts[postId].downvotes -= 1
        
        if(userVote == 0):
            disperse make(Map[U64]U64) -> userVotes[user]

        userVotes[user][postId] = 1
        posts[postId].upvotes += 1
        

// Downvote() is an invokable persistent to downvote
// the post with given id
endpoint invokable persistent Downvote(postId U64):
    memory user Address = Address(Sender)
    
    mutate posts, userVotes <- DReddit.State.posts, DReddit.State.userVotes:
        memory userVote = userVotes[user][postId]

        if(userVote == 2):
                throw "Already Downvoted"
        else if(userVote == 1):
                posts[postId].upvotes -= 1
        
        if(userVote == 0):
            disperse make(Map[U64]U64) -> userVotes[user]

        userVotes[user][postId] = 2
        posts[postId].downvotes += 1


//////////////////////
////// Read Endpoints
////////////////////

endpoint invokable GetPosts() -> (posts []Post):
    memory memPosts []Post
    observe sPosts <- DReddit.State.posts:
        for index, post in sPosts:
            memory memPost Post
            memPost = Post {
                id: post.id,
                creator: post.creator,
                imageUri: post.imageUri,
                content: post.content,
                upvotes: post.upvotes,
                downvotes: post.downvotes
            }
            append(memPosts, memPost)
    posts = memPosts

endpoint invokable GetPost(postId U64) -> (post Post):
    observe posts <- DReddit.State.posts:
        yield post posts[postId]

endpoint invokable GetUserVote(user Address, postId U64) -> (vote U64):
    observe userVotes <- DReddit.State.userVotes:
        yield vote userVotes[user][postId]